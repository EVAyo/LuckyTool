plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android' version '1.7.10'
    id 'com.google.devtools.ksp' version '1.7.10-1.0.6'
    id 'com.joom.paranoid'
}
/*序列化插件*/
apply plugin: 'kotlin-parcelize'

android {
    signingConfigs {
        release {
            storeFile file('..\\keystore\\luckyzyx.jks')
            v1SigningEnabled true
            v2SigningEnabled true
            storePassword 'luckyzyx'
            keyAlias 'luckyzyx'
            keyPassword 'luckyzyx'
        }
    }
    compileSdk 32
    def currentVersionCode = getVersionCode()

    defaultConfig {
        applicationId "com.luckyzyx.colorosext"
        minSdk 30
        targetSdk 32
        versionCode currentVersionCode
        versionName "1.0"
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            //proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            proguardFiles 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = "11"
    }
    viewBinding {
        enabled = true
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            if ('release' == variant.buildType.name) {
                outputFileName = "ColorOSExt_v${variant.versionName}.${versionCode}.apk"
            }
            if ('debug' == variant.buildType.name) {
                outputFileName = "ColorOSExt_v${variant.versionName}.${versionCode}.apk"
            }
        }
    }
}

def YukiVersion = "1.0.92"
dependencies {
    //Xposed基础
    compileOnly "de.robv.android.xposed:api:82"
    //YukiHookAPI
    implementation "com.highcapable.yukihookapi:api:"+YukiVersion
    //YukiHookAPI:KSP
    ksp "com.highcapable.yukihookapi:ksp-xposed:"+YukiVersion
    //Material主题
    implementation 'com.google.android.material:material:1.6.1'
    //约束布局
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    //快速创建settings
    implementation 'androidx.preference:preference:1.2.0'
    //OkHttp3
    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.7'
    //一个强大并且灵活的RecyclerViewAdapter
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.7'
    //ExpandableRecyclerView 使用RecyclerView 实现的折叠列表
    implementation("com.github.pokercc:ExpandableRecyclerView:0.9.3")
    //崩溃日志显示
    implementation "com.github.simplepeng.SpiderMan:spiderman:v1.1.9"
    //权限请求框架
    implementation 'com.github.getActivity:XXPermissions:15.0'
    // Kotlin 和 Java 的现代 JSON 库
    implementation('com.squareup.moshi:moshi-kotlin:1.13.0')
    // moshi注释处理
    ksp("com.squareup.moshi:moshi-kotlin-codegen:1.13.0")
}

def getVersionCode() {
    def propsFile = file('version.properties')
    def properties = new Properties()
    properties.load(new FileInputStream(propsFile))
    def vCode = properties['versionCode'].toInteger()
    properties['versionCode'] = (++vCode).toString()
    properties.store(propsFile.newWriter(), null)
    return vCode
}