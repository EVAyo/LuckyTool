plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android' version '1.7.20'
    id 'com.google.devtools.ksp' version '1.7.20-1.0.6'
    id 'com.joom.paranoid'
}

android {
    signingConfigs {
        release {
            storeFile file('..\\keystore\\luckyzyx.jks')
            v1SigningEnabled true
            v2SigningEnabled true
            storePassword 'luckyzyx'
            keyAlias 'luckyzyx'
            keyPassword 'luckyzyx'
        }
    }
    compileSdk 33
    def currentVersionCode = getVersionCode()
    namespace 'com.luckyzyx.luckytool'
    defaultConfig {
        applicationId "com.luckyzyx.luckytool"
        minSdk 30
        targetSdk 33
        versionCode currentVersionCode
        versionName "3.1_beta"
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            //proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            proguardFiles 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = "11"
    }
    viewBinding {
        enabled = true
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            if ('release' == variant.buildType.name) {
                outputFileName = "LuckyTool_v${variant.versionName}(${versionCode}).apk"
            }
            if ('debug' == variant.buildType.name) {
                outputFileName = "LuckyTool_v${variant.versionName}(${versionCode})_debug.apk"
            }
        }
    }
}

dependencies {
    //Xposed基础
    compileOnly "de.robv.android.xposed:api:82"
    //YukiHookAPI ksp
    implementation "com.highcapable.yukihookapi:api:1.1.4"
    ksp "com.highcapable.yukihookapi:ksp-xposed:1.1.4"

//    implementation files("libs/yukihookapi-1.1.0-preview.jar")
//    ksp files("libs/yukihookapi-ksp-xposed-1.1.0-preview.jar")

    //Material主题
    implementation 'com.google.android.material:material:1.6.1'
    //约束布局
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    //快速创建settings
    implementation 'androidx.preference:preference:1.2.0'
    //Navigation
    //noinspection GradleDependency
    implementation 'androidx.navigation:navigation-fragment-ktx:2.4.2'
    //noinspection GradleDependency
    implementation 'androidx.navigation:navigation-ui-ktx:2.4.2'
    //一个强大并且灵活的RecyclerViewAdapter
//    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.7'
    //崩溃日志显示
    implementation "com.github.simplepeng.SpiderMan:spiderman:v1.1.9"
    // 权限请求框架
    implementation 'com.github.getActivity:XXPermissions:16.2'
    // MoShi JSON ksp 注释处理
    implementation 'com.squareup.moshi:moshi:1.14.0'
    ksp 'com.squareup.moshi:moshi-kotlin-codegen:1.14.0'
    //ColorPickerView
    api "com.github.skydoves:colorpickerview:2.2.4"
    //Rikka
    implementation "dev.rikka.rikkax.core:core:1.4.1"
    //kotlin协程
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4")
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'
    //OkHttp相关
    //noinspection GradleDependency
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
    implementation 'com.github.liangjingkanji:Net:3.5.1'
}

def getVersionCode() {
    def propsFile = file('version.properties')
    def properties = new Properties()
    properties.load(new FileInputStream(propsFile))
    def vCode = properties['versionCode'].toInteger()
    properties['versionCode'] = (++vCode).toString()
    properties.store(propsFile.newWriter(), null)
    return vCode
}