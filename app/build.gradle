plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android' version '1.7.10'
    id 'com.google.devtools.ksp' version '1.7.10-1.0.6'
    id 'com.joom.paranoid'
}

android {
    signingConfigs {
        release {
            storeFile file('..\\keystore\\luckyzyx.jks')
            v1SigningEnabled true
            v2SigningEnabled true
            storePassword 'luckyzyx'
            keyAlias 'luckyzyx'
            keyPassword 'luckyzyx'
        }
    }
    compileSdk 32
    def currentVersionCode = getVersionCode()

    defaultConfig {
        applicationId "com.luckyzyx.luckytool"
        minSdk 30
        targetSdk 33
        versionCode currentVersionCode
        versionName "1.6"
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            //proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            proguardFiles 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = "11"
    }
    viewBinding {
        enabled = true
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            if ('release' == variant.buildType.name) {
                outputFileName = "LuckyTool_v${variant.versionName}.${versionCode}.apk"
            }
            if ('debug' == variant.buildType.name) {
                outputFileName = "LuckyTool_v${variant.versionName}.${versionCode}.apk"
            }
        }
    }
}

dependencies {
    //Xposed基础
    compileOnly "de.robv.android.xposed:api:82"
    //YukiHookAPI ksp
    implementation "com.highcapable.yukihookapi:api:1.0.92"
    ksp "com.highcapable.yukihookapi:ksp-xposed:1.0.92"
    //Material主题
    implementation 'com.google.android.material:material:1.6.1'
    //约束布局
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    //快速创建settings
    implementation 'androidx.preference:preference:1.2.0'
    //Navigation
    implementation 'androidx.navigation:navigation-fragment-ktx:2.4.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.4.2'
    //一个强大并且灵活的RecyclerViewAdapter
//    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.7'
    //崩溃日志显示
    implementation "com.github.simplepeng.SpiderMan:spiderman:v1.1.9"
    //权限请求框架
//    implementation 'com.github.getActivity:XXPermissions:15.0'
    // MoShi JSON ksp 注释处理
    implementation 'com.squareup.moshi:moshi:1.13.0'
    ksp "com.squareup.moshi:moshi-kotlin-codegen:1.13.0"
    //ColorPickerView
    api "com.github.skydoves:colorpickerview:2.2.4"
    //Rikka
    implementation "dev.rikka.rikkax.core:core:1.4.1"
    //kotlin协程
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4")
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'
}

def getVersionCode() {
    def propsFile = file('version.properties')
    def properties = new Properties()
    properties.load(new FileInputStream(propsFile))
    def vCode = properties['versionCode'].toInteger()
    properties['versionCode'] = (++vCode).toString()
    properties.store(propsFile.newWriter(), null)
    return vCode
}