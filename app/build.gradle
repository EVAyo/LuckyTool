plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android' version '1.8.10'
    id 'com.google.devtools.ksp' version '1.8.10-1.0.9'
    id 'com.joom.paranoid'
}

android {
    signingConfigs {
        release {
            v1SigningEnabled true
            v2SigningEnabled true
            storeFile file('..\\keystore\\luckyzyx.jks')
            storePassword 'luckyzyx'
            keyAlias 'luckyzyx'
            keyPassword 'luckyzyx'
        }
    }
    compileSdk 33
    namespace 'com.luckyzyx.luckytool'
    def currentVersionCode = getVersionCode()
    defaultConfig {
        applicationId "com.luckyzyx.luckytool"
        minSdk 30
        targetSdk 33
        versionCode currentVersionCode
        versionName "4.3.8"
        buildConfigField("String", "APP_CENTER_SECRET", "\"${getAppCenterSecret()}\"")
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            //proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            proguardFiles 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = "11"
    }
    viewBinding {
        enabled = true
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            if ('release' == variant.buildType.name) {
                outputFileName = "LuckyTool_v${variant.versionName}(${versionCode}).apk"
            }
            if ('debug' == variant.buildType.name) {
                outputFileName = "LuckyTool_v${variant.versionName}(${versionCode})_debug.apk"
            }
        }
    }
    androidResources.additionalParameters("--allow-reserved-package-id", "--package-id", "0x64")
}

dependencies {
//    implementation fileTree(dir: "libs", include: ["*.jar"])

    //Xposed基础
    compileOnly "de.robv.android.xposed:api:82"
    //YukiHookAPI ksp
    implementation "com.highcapable.yukihookapi:api:1.1.8"
    ksp "com.highcapable.yukihookapi:ksp-xposed:1.1.8"

//    implementation files("libs/yukihookapi-1.1.5-fix-pkgname-equals.jar")
//    ksp files("libs/yukihookapi-ksp-xposed-1.1.5-beta2.jar")

    //Material主题
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1'
    //约束布局
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    //快速创建Settings
    implementation 'androidx.preference:preference:1.2.0'
    //下拉刷新控件
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    //Navigation
    //noinspection GradleDependency
    implementation 'androidx.navigation:navigation-fragment-ktx:2.4.2'
    //noinspection GradleDependency
    implementation 'androidx.navigation:navigation-ui-ktx:2.4.2'
    //一个强大并且灵活的RecyclerViewAdapter
//    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.11'
    //崩溃日志显示
    implementation "com.github.simplepeng.SpiderMan:spiderman:v1.1.9"
    // MoShi JSON ksp 注释处理
    implementation 'com.squareup.moshi:moshi:1.14.0'
    ksp 'com.squareup.moshi:moshi-kotlin-codegen:1.14.0'
    //Rikka
    implementation "dev.rikka.rikkax.core:core:1.4.1"
    //kotlin协程
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4"
    //OkHttp相关
    //noinspection GradleDependency
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
    implementation 'com.github.liangjingkanji:Net:3.5.2'
    //Apache DigestUtils md5 sha256
//    implementation 'com.google.firebase:firebase-crashlytics-buildtools:2.9.2'
    //libsu
    implementation "com.github.topjohnwu.libsu:core:5.0.4"
    implementation "com.github.topjohnwu.libsu:service:5.0.4"

    //Microsoft AppCenter
    def appCenterSdkVersion = '5.0.0'
    implementation "com.microsoft.appcenter:appcenter-analytics:${appCenterSdkVersion}"
    implementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"
}

def getVersionCode() {
    def propsFile = file('version.properties')
    def properties = new Properties()
    properties.load(new FileInputStream(propsFile))
    def vCode = properties['versionCode'].toInteger()
    properties['versionCode'] = (++vCode).toString()
    properties.store(propsFile.newWriter(), null)
    return vCode
}

String getAppCenterSecret() {
    def fileName = '../.secret/APP_CENTER_SECRET'
    def content = ''
    if (file(fileName).exists()) file(fileName).eachLine { content = it }
    return content
}